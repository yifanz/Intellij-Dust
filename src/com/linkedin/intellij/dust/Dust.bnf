{
  parserClass="com.linkedin.intellij.dust.parser.DustParser"
  stubParserClass="com.linkedin.intellij.dust.parser.GeneratedParserUtilBase"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="Dust"
  psiImplClassSuffix="Impl"
  psiPackage="com.linkedin.intellij.dust.psi"
  psiImplPackage="com.linkedin.intellij.dust.psi.impl"

  elementTypeHolderClass="com.linkedin.intellij.dust.psi.DustTypes"
  elementTypeClass="com.linkedin.intellij.dust.psi.DustElementType"
  tokenTypeClass="com.linkedin.intellij.dust.psi.DustTokenType"
}

dustFile ::= statements

statements ::= (COMMENT|tag_block|tag|json|HTML|CRLF)* { pin=1 }

json ::= LD (json|tag|HTML|CRLF|STRING|IDENTIFIER)* RD

private statements_block ::= (statements? else_tag statements?) | statements

tag_block ::= open_tag statements_block close_tag { pin=1 }

tag ::= (self_close_tag|key_tag)

key_tag ::= LD tag_name index_deref? context* tag_content RD

open_tag ::= (SECTION|EXISTANCE|NOT_EXISTANCE|HELPER|PARTIAL|INLINE_PARTIAL|BLOCK) CRLF? tag_name index_deref? context* tag_content RD

else_tag ::= ELSE tag_name tag_content RD

close_tag ::= CLOSE tag_name index_deref? tag_content RD

self_close_tag ::= (HELPER|PARTIAL|INLINE_PARTIAL|BLOCK|CLOSE|ELSE|LD) tag_name context* tag_content SLASH_RD

tag_name ::= (path|STRING|STRING_SINGLE|IDENTIFIER|PERIOD)

index_deref ::= LB (NUMBER|IDENTIFIER+|path) RB

context ::= COLON (path|STRING|STRING_SINGLE|IDENTIFIER|PERIOD)

tag_content ::= attribute* filter*

path::= (PERIOD? IDENTIFIER (PERIOD IDENTIFIER)*) | PERIOD

attribute ::= CRLF? IDENTIFIER EQUAL (path|attrString|NUMBER|IDENTIFIER) CRLF?

attrString ::= (STRING_START ((tag_block|tag|i18nFormat) | STRING+)+ STRING_END) | (STRING_START STRING_END)

i18nFormat ::= LD (tag|NUMBER|IDENTIFIER|HTML|PIPE)* RD

filter ::= PIPE IDENTIFIER